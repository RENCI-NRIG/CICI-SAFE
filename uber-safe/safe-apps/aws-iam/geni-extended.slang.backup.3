//defenv Selfie() :-
//  spec('Load the principal keypair'),
//  principal($1).

//defenv GeniRoot() :- u'jT9Gocrdu_KIm3Vb9eQc2a3lNWmtHp_AX1b3nD_cq-o'.
defenv GeniRoot() :- u'IBpC0ljPorTuO85iJ-9GIxkXyDlOHAfFwcKfGtj-h2U'.

defenv MyAnchorSet() :- anchorSet().

//
// Identity set (apply to all participants)
//

defcon makeMyIdentitySet(?CN) :-
  spec("Construct the identity set for self"),
  {
    principal($SelfKey) :- spec("identity set for $CN").
  }.

defcon makeMySubjectSet() :-
  spec("Construct the subject set for self"),
  {
    link($Self).
    label("subject($Self)").
  }.

defcon addTokenToSubjectSet(?Token) :-
  spec("Add a token to the user's subject set. Invoke after the user gets a delegation"),
  {
    link($Token).
    label("subject($Self)").
  }.

defpost postIdSet(?CN) :- [makeMyIdentitySet(?CN)].

defpost postSubjectSet() :- [makeMySubjectSet()].

defpost updateSubjectSet(?Token) :- [addTokenToSubjectSet(?Token)].


//
// Federation policy: trust structure and user/PI registration
//

defcon trustStructure() :-
  spec('Federation trust structure: adopted by all participants'),
  {
    memberAuthority(?MA) :- geniRoot(?Geni), ?Geni: memberAuthority(?MA).
    sliceAuthority(?SA) :- geniRoot(?Geni), ?Geni: sliceAuthority(?SA).
    projectAuthority(?PA) :- geniRoot(?Geni), ?Geni: projectAuthority(?PA).
    geniProxy(?GeniProxy) :- geniRoot(?Geni), ?Geni: geniProxy(?GeniProxy).
    aggregate(?Agg) :- geniRoot(?Geni), ?Geni: aggregate(?Agg).
    gmoc(?GeniMOC) :- geniRoot(?Geni), ?Geni: gmoc(?GeniMOC).
    label('geni/trust-structure').
  }.

defcon registeredUserPolicy() :-
  spec('Federation policy for registered users and PIs: adopted by all participants'),
  {
    geniUser(?User) :- memberAuthority(?MA), ?MA: geniUser(?User).
    geniPI(?User) :- memberAuthority(?MA), ?MA: geniPI(?User).
    label('geni/registered-user').
  }.

definit trustStructure(), registeredUserPolicy().

//
// Federation root: endorsements
//

defcon endorseMA(?MA) :-
  spec('endorse a member authority'),
  {
    memberAuthority($MA) :- spec("geni endorses a member authority $MA").
    label("ma/$MA").
  }.

defcon endorsePA(?PA) :-
  spec('endorse a project authority'),
  {
    projectAuthority($PA) :- spec("geni endorses a project authority $PA").
    label("pa/$PA").
  }.

defcon endorseSA(?SA) :-
  spec('endorse a slice authority'),
  {
    sliceAuthority($SA) :- spec("geni endorses a slice authority $SA").
    label("sa/$SA").
  }.

defcon endorseCP(?CP) :-
  spec('endorse an aggregate'),
  {
    aggregate($CP) :- spec("geni endorses an aggregate $CP").
    label("aggregate/$CP").
  }.

defpost postMAEndorsement(?MA) :- [endorseMA(?MA)].
defpost postPAEndorsement(?PA) :- [endorsePA(?PA)].
defpost postSAEndorsement(?SA) :- [endorseSA(?SA)].
defpost postCPEndorsement(?CP) :- [endorseCP(?CP)].


//
// MA: endorsements for registered users and PIs.
//

defcon endorsePI(?PI) :-
  spec('Endorse a geni PI'),
  ?SubjectSetRef := label($Self, "subject($Self)"),
  {
    geniUser($PI).
    geniPI($PI).
    link($SubjectSetRef).
    label("pi/$PI").
  }.

defcon endorseUser(?User) :- 
  spec('Endorse a geni user'),
  ?SubjectSetRef := label($Self, "subject($Self)"),
  {
    geniUser($User).
    link($SubjectSetRef).
    label("user/$User").
  }.

defpost postPIEndorsement(?PI) :- [endorsePI(?PI)].
defpost postUserEndorsement(?User) :- [endorseUser(?User)].


//
// PA: projects and their policy sets
//

defcon projectMemberSet() :- 
  {
    link("standard-user-group-membership-policy").  // Post basic user group policy first

    memberPrivilege(?User, ?Project, instantiate, ?Delegatable) :- 
      member(?User, ?Project, ?Delegatable).

    memberPrivilege(?User, ?Project, ?Priv, ?Delegatable) :-
      delegatesMemberPrivilege(?Delegator, ?User, ?Project, ?Priv, ?Delegatable),
      memberPrivilege(?Delegator, ?Project, ?Priv, true).

    delegatesMemberPrivilege(?Delegator, ?User, ?Project, ?Priv, ?Delegatable) :-
      ?Delegator: delegateMemberPrivilege(?User, ?Project, ?Priv, ?Delegatable).
 
    label('pa/standard-project-membership') :- 
      spec('Standard project membership policy').
  }.

defpost postMemberSet() :- 
  [projectMemberSet()].

defpost postProjectMembershipDelegation(?SubjectId, ?ProjectId, ?Delegatable) :- 
  [delegateGroupMembership(?SubjectId, ?ProjectId, ?Delegatable)].


//
// PA authZ policy for project creation.
//
defcon projectPolicySet() :-
  {
    approveProject(?Owner) :-
      geniPI(?Owner).
    label('pa/standard-project-operations').
  }.

definit projectPolicySet().

defpost postProjectSet(?SubjectId, ?ProjectId, ?ProjectMembershipPolicyRef) :- 
  [createGroupSet(?SubjectId, ?ProjectId, ?ProjectMembershipPolicyRef, project)].

//
// SA: slices and their policy sets
//

defcon standardSliceControlSet() :- 
  {

    link("standard-user-group-membership-policy").  // Post basic user group policy first
 
    controlPrivilege(?Subject, ?Slice, instantiate, ?Delegator) :- 
      controls(?Subject, ?Slice, ?Delegator).
 
    controlPrivilege(?Subject, ?Slice, ?Priv, ?Delegatable) :- 
      delegatesControlPrivilege(?Delegator, ?Subject, ?Slice, ?Priv, ?Delegatable),
      controlPrivilege(?Delegator, ?Slice, ?Priv, true).

    delegatesControlPrivilege(?Delegator, ?Subject, ?Slice, ?Priv, ?Delegatable) :-
      ?Delegator: delegatesControlPrivilege(?Subject, ?Slice, ?Priv, ?Delegatable).

    label('sa/standard-slice-control') :- 
      spec('Standard slice control policy').
  }.

defpost postStandardSliceControlSet() :- 
  [standardSliceControlSet()].


defcon standardSliceDefaultPrivilegeSet() :-
  {
    link("sa/standard-slice-control").  // Post standard slice control set first

    controlPrivilege(?Subject, ?Slice, info, ?Delegatable) :-
      slice(?Slice, ?Project, standard),
      ?PA := rootPrincipal(?Project),
      projectAuthority(?PA),
      ?PA: group(?Project, project, standard),
      ?PA: memberPrivilege(?Subject, ?Project, info, ?Delegatable).
 
    controlPrivilege(?Subject, ?Slice, stop, true) :- 
      slice(?Slice, ?Project, standard),
      ?PA := rootPrincipal(?Project),
      projectAuthority(?PA),
      ?PA: group(?Project, project, standard),
      ?PA: owner(?Subject, ?Project).
 
    controlPrivilege(?Subject, ?Slice, info) :- 
      slice(?Slice, ?Project, standard),
      gmoc(?Subject).
 
    controlPrivilege(?Subject, ?Slice, stop) :- 
      slice(?Slice, ?Project, standard),
      gmoc(?Subject).
 
    label('sa/standard-slice-privilege') :- 
      spec('Standard slice default privilege policy').
  }.

defpost postStandardSliceDefaultPrivilegeSet() :- 
  [standardSliceDefaultPrivilegeSet()].

defpost postSliceControl(?SubjectId, ?SliceId, ?Delegatable) :- 
  [delegateGroupMembership(?SubjectId, ?SliceId, ?Delegatable)].


//
// SA authZ policy for slice creation.
//
defcon slicePolicySet() :-
  {
    approveSlice(?Subject, ?Project) :-
      ?PA := rootPrincipal(?Project),
      projectAuthority(?PA),
      ?PA: group(?Project, project, standard),
      ?PA: memberPrivilege(?Subject, ?Project, instantiate, _),
      geniUser(?Subject).

    label('sa/standard-slice-operations').
  }.

definit slicePolicySet().

//
// A newly approved slice is created as a set with links to its policy sets.
//

defcon createSliceSet(?SliceId, ?ProjectId) :-
  ?SliceGroupSetRef := label($Self, "groups/?SliceId"),
  {
    link($SliceGroupSetRef).
    slice($SliceId, $ProjectId, standard).
    label("slice/$SliceId").
  }.

defpost postSliceSet(?SubjectId, ?SliceId, ?ProjectId, ?SliceControlPrivRef) :- 
  [createGroupSet(?SubjectId, ?SliceId, ?SliceControlPrivRef, slice), createSliceSet(?SliceId, ?ProjectId)].


//
// Aggregate
//

defcon aggregatePolicySet() :- 
  {
     approveSliver(?Subject, ?Slice) :-
       ?SA := rootPrincipal(?Slice),
       ?SA: slice(?Slice, ?Project, standard),
       sliceAuthority(?SA), 
       ?SA: controlPrivilege(?Subject, ?Slice, instantiate, _),
       geniUser(?Subject).
 
     label('aggregate/standard-slice-operations').
  }.

definit aggregatePolicySet().

defcon sliverAccessPolicySet() :-
  {
    approveSliverAccess(?SubjectId, ?SliverId) :-
      ?CP := rootPrincipal(?SliverId),
      aggregate(?CP),
      ?CP: sliverOf(?SliverId, ?SliceId, standard),
      ?SA := rootPrincipal(?SliceId),
      ?SA: member(?SliceMember, ?SliceId),
      ?SliceMember: sliverPrivilege(?SubjectId, ?SliverId). // Acls have been checked before being posted
      geniUser(?SubjectId).

    label('aggregate/standard-sliver-access').
  }.

definit sliverPolicySet().

defcon createSliverAclsSet(?SliverId) :-
  spec("For aggregate.postSliverAclsSet"),
  {
     label("acls($SliverId)"). 
  }.

defpost postSliverAclsSet(?SliverId) :- 
  [createSliverAclsSet(?SliverId)].

defcon addTokenToSliverAclsSet(?Token, ?SliverId) :-
  spec("For aggregate.updateSliverAcls"),
  {
     link($Token).
     label("acls($SliverId)"). 
  }.

defpost updateSliverAclsSet(?Token, ?SliverId) :- 
  [addTokenToSliverAclsSet(?Token, ?SliverId)].

defcon addSliverAcl(?SliverId, ?GroupId) :-
  spec("For SliceMember.addSliverAcl"),
  {
     //sliverPrivilege($SliverId, $GroupId).
     sliverPrivilege(?SubjectId, $SliverId) :- ?GRoot := rootPrincipal($GroupId), ?GRoot: member(?SubjectId, $GroupId).
     label("acls($SliverId)"). // Update this link to SliverId's Acls set
  }.

defpost postSliverAcl(?SliverId, ?GroupId) :- 
  [addSliverAcl(?SliverId, ?GroupId)].


defcon createSliverSet(?SliverId, ?SliceId, ?ZoneId, ?VlanTag) :-
  spec("For aggregate.createSliverSet"),
  ?SubjectSetRef := label($Self, "subject($Self)"),
  ?AclsSetRef := label($Self, "acls($SliverId)"),
  ?SA := rootAuthority(?SliceId),
  ?SliceSetRef := label(?SA, "slice/$SliceId"),
  ?ZoneSetRef := label($Self, "zones/$ZoneId"),
  {
    sliverOf($SliverId, $SliceId, standard).
    stitchport($SliverId, $ZoneId, $VlanTag).
    link($SubjectSetRef).
    link($AclsSetRef).
    link($SliceSetRef).
    link($ZoneSetRef).
    label("slivers/$SliverId").
  }.

defpost postSliverSet(?SubjectId, ?SliverId, ?SliceId) :- 
  [createSliverSet(?SubjectId, ?SliverId, ?SliceId)].


defcon addAdjacentCP(?AdjacentCP) :-
  spec("For aggregate.addAdjacentCP"),
  {
    adjacentAggregate($AdjacentCP).
    label("AdjacentCPList").  // update this link to the aggregate's subject set
  }.

defpost updateAdjacentCPList(?AdjacentCP) :- 
  [addAdjacentCP(?AdjacentCP)].


defcon createZoneSet(?ZoneId) :-
  spec("For aggregate.createZoneSet"),
  ?SubjectSetRef := label($Self, "subject($Self)"),
  {
    zone($ZoneId, standard).
    link($SubjectSetRef).
    label("zones/$ZoneId").
  }.

defpost postZoneSet(?ZoneId) :- 
  [createZoneSet(?ZoneId)].


//
// Guards
// There are three for primary GENI interfaces:
// pa.createProject, sa.createSlice, agg.createSliver.
// AuthZ for other agg slice operations is similar to approveSliver.
//

defguard createProject() :-
  spec("Guard for ProjectAuthority.createProject"),
  ?PolicyRef := label("pa/standard-project-operations"),
  {
    link($MyAnchorSet).
    link($PolicyRef).
    link($BearerRef).
    approveProject($Subject)?
  }.

defguard createSlice(?ProjectId) :-
  spec("Guard for SliceAuthority.createSlice"),
  ?PolicyRef := label("sa/standard-slice-operations"),
  {
    link($MyAnchorSet).
    link($PolicyRef).
    link($BearerRef). 
    approveSlice($Subject, $ProjectId)?
  }.

defguard createSliver(?SliceId) :-
  spec("Guard for Aggregate.createSliver"),
  ?PolicyRef := label("aggregate/standard-slice-operations"),
  {
    link($MyAnchorSet).
    link($PolicyRef).
    link($BearerRef). 
    approveSliver($Subject, $SliceId)?
  }.

defguard accessSliver(?SliverId) :-
  spec("Guard for Aggregate.accessSliver"),
  ?PolicyRef := label("aggregate/standard-sliver-access"),
  {
    link($MyAnchorSet).
    link($PolicyRef).
    link($BearerRef). 
    approveSliverAccess($Subject, $SliverId)?
  }.

defguard stitchSlivers(?SrcSliverId, ?PeerSliverId) :-
  spec("Guard for Aggregate.stitchSlivers"),
  ?SrcAggregate := rootPrincipal(?SrcSliverId),
  ?SrcSliverSetRef := label(?SrcAggregate, "slivers/$SliverId"),
  ?PeerAggregate :-= rootPrincipal(?PeerSliverId), 
  ?PeerSliverSetRef := label(?PeerAggregate, "slivers/$SliverId"), 
  ?PolicyRef := label("aggregate/standard-sliver-access"),
  {
    link($MyAnchorSet).
    link($BearerRef). 
    link($SrcSliverSetRef).
    link($PeerSliverSetRef).
    link($PolicyRef).
  
    validZone(?SliverId) :-
      ?Aggregate := rootPrincipal(?SliverId),
      ?Aggregate: stitchport(?SliverId, ?ZoneId, ?VlanTag),
      ?Aggregate: zone(?ZoneId, standard).

    approveStitching(?SrcSliverId, ?PeerSliverId) :-
      validZone(?SrcSliverId),
      validZone(?PeerSliverId),
      ?SrcCP := rootPrincipal(?SrcSliverId),
      adjacentAggregate(?SrcCP),
      ?SrcCP: sliverOf(?SrcSliverId, ?SrcSliceId, standard),
      ?SrcSA := rootPrincipal(?SrcSliceId),
      ?SrcSA: member($Subject, ?SrcSliceId),
      approveSliverAccess($Subject, ?PeerSliverId).

    approveStitching($SrcSliverId, $PeerSliverId)?
  }.


defguard installSliverAcl(?SliverId) :-
  ?SliverSetRef := label($Self, "slivers/$SliverId"),
  {
     link($BearerRef).
     link($SliverSetRef).

     approveSliverAcl(?SubjectId, ?SliverId) :-
       sliverOf(?SliverId, ?SliceId, standard),
       ?SA := rootPrincipal(?SliceId),
       ?SA: member(?SubjectId, ?SliceId),
       geniUser(?SubjectId). 

     approveSliverAcl($Subject, $SliverId)?
  }.


//
// Group authority
//

defcon userGroupMemberSet() :-
  spec('Policies on group membership'),
  {
    member(?User, ?Group) :-
      member(?User, ?Group, _).

    member(?User, ?Group, true) :-
      owner(?User, ?Group).

    member(?User, ?Group, ?Delegatable) :-
      groupMember(?Group, ?User, ?Delegatable).

    member(?Group, ?User, ?Delegatable) :-
      ?Delegator: delegateMember(?User, ?Group, ?Delegatable),
      member(?Delegator, ?Group, true).

    member(?User, ?Group, ?Delegatable) :-
      nestGroup(?Group, ?ToGroup, true),
      ToGroupRoot := rootPrincipal(?ToGroup),
      ToGroupRoot: member(?User, ?ToGroup, ?Delegatable).

    member(?User, ?Group, ?Delegatable) :-
      nestGroup(?Group, ?ToGroup, false),
      ?ToGroupRoot := rootPrincipal(?ToGroup),
      ?ToGroupRoot: groupMember(?ToGroup, ?User, ?Delegatable).

    label('standard-user-group-membership-policy').
  }.
                                                  

defcon createGroupSet(?SubjectId, ?GroupId, ?PolicyRef, ?Type) :-
  spec('Construct a set for a group'),
  ?SubjectSetRef := label($Self, "subject($Self)"),
  {
    owner($SubjectId, $GroupId).
    group($GroupId, $Type, standard). 
    link($PolicyRef).
    link($SubjectSetRef).
    label("groups/$GroupId").  // Update the subject set of $SubjectId
  }.

defpost postGroupSet(?SubjectId, ?GroupId, ?PolicyRef, ?Type) :- 
  [createGroupSet(?SubjectId, ?GroupId, ?PolicyRef, ?Type)].

defcon addTokenToGroupSet(?Token, ?GroupId) :-
  spec("Add a link to a group's ID set. Invoked after delegation to this group"),
  {
    link($Token).
    label("groups/$GroupId").
  }.

defpost updateGroupSet(?Token, ?GroupId) :- [addTokenToGroupSet(?Token, ?GroupId)].

defcon addGroupMember(?GroupId, ?SubjectId, ?Delegatable) :-
  spec('Add a user into a group'),
  ?GroupSetRef := label($Self, "groups/?GroupId"),
  {
    groupMember($GroupId, $SubjectId, $Delegatable).
    link($GroupSetRef).
    label("groupMember/$GroupId/$SubjectId").     (* this link should be saved in the subject set of ?User *)
  }.

defpost postGroupMember(?GroupId, ?SubjectId, ?Delegatable) :- [addGroupMember(?GroupId, ?SubjectId, ?Delegatable)].

defcon delegateGroup(?GroupId, ?ToGroupId, ?Delegatable) :-
  spec('Delegate administration to another group'), (* Membership is granted if membership of that group is granted *)
  ?GroupSetRef := label($Self, "groups/?GroupId"),
  {
    nestGroup($GroupId, $ToGroupId, $Delegatable).
    link($GroupSetRef).
    label("delegateGroup/$GroupId/$ToGroupId").  (* this link should be stored in the ?ToGroupId set *)
  }.

defpost postGroupDelegation(?GroupId, ?ToGroupId, ?Delegatable) :- [delegateGroup(?GroupId, ?ToGroupId, ?Delegatable)].


//
// User (group member)
//

defcon delegateGroupMembership(?SubjectId, ?GroupId, ?Delegatable) :-
  spec('Delegate group membership to another user'),
  ?SubjectSetRef := label("subject($Self)"),
  {
    delegateMember($SubjectId, $GroupId, $Delegatable).
    link($SubjectSetRef).
    label("group membership delegation $SubjectId $GroupId").  (* this link should be saved in the subject set of ?SubjectId *)
  }.

defpost postUserGroupMembershipDelegation(?SubjectId, ?GroupId, ?Delegatable) :- [delegateGroupMembership(?SubjectId, ?GroupId, ?Delegatable)].



//
// Local example
//

defcon fedEndorsementStub() :-
  ?E1 := endorseMA($Self),
  ?E2 := endorsePA($Self),
  ?E3 := endorseSA($Self),
  ?E4 := endorseCP($Self),
  {
    link($E1).
    link($E2).
    link($E3).
    link($E4).
    label("geni/endorse-self-stub").
  }.


//
// Disabled the following from anchorSet()
// Caused infinite loops
// ?FedEndorseRef := fedEndorsementStub(),
//  link($FedEndorseRef).
//

defcon anchorSet() :-
  ?FedStructureRef := label("geni/trust-structure"),
  ?FedUserRef := label("geni/registered-user"),   
  {
    geniRoot($GeniRoot).
    link($FedStructureRef).
    link($FedUserRef).   
    label("geni/anchor-set").
  }.
