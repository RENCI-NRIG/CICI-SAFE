defenv Selfie() :-
  spec('Load the principal keypair'),
  principal($1).
  (* principal('/home/qiang/Desktop/safe-new-version-benchmark/safe/safe-apps/naming-service/keys/na1.pem'). *)

(**
 *
 * Name authority records:
 *   NA(?DomainName, ?NameAuthority)
 *
 * Object records:
 *   O(?Name, ?Scid)
 *
 *)

defcon makeMyIdentitySet(?CN) :-
  spec('Construct an identity set for self'),
  {
    principal($SelfKey) :- spec("identity set for $CN").
  }.

defcon addSubdomain(?Subdomain, ?NameAuthority) :-
  spec('Add a subdomain with a name authority'),
  {
    NA($Subdomain, $NameAuthority).
    name('').
  }.

defcon addName(?Name, ?Scid) :- 
  spec('Add a name'),
  {
    O($Name, $Scid).
    name('').
  }.

defcon addALinkToIdSet(?LinkRef) :-
  spec("Add a link to ID set"),
  {
    link($LinkRef).
    name('').
  }.

defpost postSubdomain(?Subdomain, ?NameAuthority) :- [addSubdomain(?Subdomain, ?NameAuthority)].

defpost postName(?Name, ?Scid) :- [addName(?Name, ?Scid)].

defpost postIdSet(?CN) :- [makeMyIdentitySet(?CN)].

defpost postALinkToIdSet(?LinkRef) :- [addALinkToIdSet(?LinkRef)].
